plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom'
}
base {
    archivesName = "${mod_id}"
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")
    modImplementation "curse.maven:load-my-resources-fabric-534835:4118347"
    modImplementation "maven.modrinth:modmenu:4.1.2"
    implementation fileTree('libs') { include '*.jar' }
    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.3.1")))
    include(implementation 'com.github.Keksuccino:japng:0.5.3')
    include(implementation 'com.github.rtyley:animated-gif-lib-for-java:animated-gif-lib-1.7')
    modImplementation "curse.maven:melody-938643:4970117"
    //This is only to fix the missing dependency error in-dev
    implementation group: 'ca.weblite', name: 'java-objc-bridge', version: '1.1'
//    modImplementation "curse.maven:applied-energistics-2-wireless-terminals-459929:4854629"
//    modImplementation "curse.maven:applied-energistics-2-223794:5014666"
//    modImplementation "curse.maven:cloth-config-348521:4973443"
//    include modApi('teamreborn:energy:2.3.0') {
//        transitive = false
//    }
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run_client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run_server")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
